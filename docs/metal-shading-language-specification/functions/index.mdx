# 函数

<OrigninalPDF title="5.1 Functions" />

Metal 1 或更高版本支持每个操作系统的内核、顶点和片段属性。Metal 2.3 或更高版本支持 C++ 属性：

- `[[vertex]]` or `vertex`
- `[[fragment]]` or `fragment`
- `[[kernel]]` or `kernel`
- `[[visible]]`
- `[[stitchable]]`
- `[[intersection(...)]]`
- `[[object]]`
- `[[mesh]]`

通过在函数的开头添加这些函数属性之一，使函数可以被 Metal API 访问，这使其成为 _限定_ 函数。内核函数、顶点函数和片段函数不能在不触发编译错误的情况下相互调用，但它们可以调用其它使用 `[[visible]]` 属性的函数。他们还可以通过调用 `intersect()` 来调用具有 `[[intersection(...)]]` 属性的函数（请参阅第 6.18.2 节）。

在 Metal 2.2 之前，Metal 编译器忽略内核、顶点和片段函数的命名空间标识符。在 Metal 2.2 及更高版本中，如果你在命名空间中声明限定函数，则每次将其引用到 Metal Framework API 时，都必须包含命名空间标识符和函数名称。此示例在不同的命名空间中声明两个内核函数。

```cpp
namespace outer {
    [[kernel]] void functionA() { ... }
    namespace inner {
        [[kernel]] void functionB() { ... }
    }
}
```

通过在函数名称前面加上命名空间的标识符，后跟两个冒号来引用命名空间中的函数。

```cpp
Outer::functionA
```

类似地，通过按顺序在函数名称前加上所有命名空间并用两个冒号分隔每个命名空间来引用嵌套命名空间中的函数。

```cpp
Outer::inner::functionB
```

## 顶点函数

你可以声明顶点，或者自 Metal 2.3 起 [[vertex]] 属性仅适用于图形函数。Metal 为顶点流中的每个顶点执行顶点函数并生成每个顶点的输出。以下示例显示了声明顶点的语法。

```cpp
vertex void
my_vertex_func(...)
{ ... }

[[vertex]] void
vertex_func2(...)
{ ... }
```

对于顶点函数，返回类型标识函数生成的输出。如果顶点函数不生成输出，它将返回 `void` 并且只能在禁用光栅化的渲染管道中使用。

### 曲面细分后顶点函数

所有操作系统：自 Metal 1.2 起支持曲面细分后顶点函数（补丁属性）。

曲面细分后顶点函数计算固定函数曲面细分器生成的面片上每个表面样本的顶点数据。曲面细分后顶点函数的输入为：

- 每个补丁数据。
- 修补控制点数据。
- 细分器阶段输出（补丁上的标准化顶点位置）。

细分后顶点函数生成细分三角形的最终顶点数据。例如，为了向渲染的几何体添加额外的细节（例如位移映射值），曲面细分后顶点函数可以对纹理进行采样，以通过位移值修改顶点位置。

执行后曲面细分顶点函数后，曲面细分的图元将被光栅化。

曲面细分后顶点函数是使用普通顶点函数属性标识的顶点函数。

### 补丁类型和每个补丁的控制点数量

曲面细分后顶点函数需要 `[[patch]]` 属性。

对于 macOS，`[[patch(patch-type, N)]]` 属性必须指定面片类型（`patch-type` 为 `quad` 或 `triangle`）和面片中控制点的数量（N 需要是以下值） 0 到 32）。对于 iOS，需要指定 `patch-type`，但控制点的数量是可选的。

如果在曲面细分后顶点函数中指定了控制点的数量，则该数量必须与提供给 `drawPatches` 或 `drawIndexedPatches` API 的控制点的数量匹配。

例子：

```cpp
[[patch(quad)]]
[[vertex]] vertex_output
my_post_tessellation_vertex(...)
{...}

[[patch(quad, 16)]]
[[vertex]] vertex_output
my_bezier_vertex(...)
{...}
```

## 片段函数

你可以声明 `fragment` 或自 Metal 2.3 起 `[[fragment]]` 属性仅用于图形函数。Metal 为片段流中的每个片段及其关联数据执行片段函数，并生成每个片段的输出。以下示例显示了使用 `fragment` 属性声明片段函数的语法。

```cpp
[[fragment]] void my_fragment_func(...) {
    // ...
}

[[fragment]] void fragment_func2(...) {
    // ...
}
```

对于图形函数，返回类型标识函数生成的输出是按顶点还是按片段。如果片段函数不生成输出，则返回 `void`。

要请求在片段函数执行之前执行片段测试，请将 `[[early_fragment_tests]]` 函数属性与片段函数结合使用，如下例所示。

```cpp
[[early_fragment_tests]] fragment float4 my_fragment(...) {
    // ...
}
```

如果使用 `[[early_fragment_tests]]` 属性声明的片段函数的返回类型包含深度或模板值，则这是一个错误；也就是说，如果此片段函数的返回类型包含使用 `[[depth(depth_attribute)]]` 或 `[[stencil]]` 属性声明的元素。

将 `[[early_fragment_tests]]` 属性与任何非片段函数的函数一起使用是错误的；也就是说，没有用 `fragment` 属性声明。

## 计算函数（内核）

计算函数（也称为 "kernel" ）是在 1、2 或 3 维网格上执行的数据并行函数。以下示例显示了使用 `kernel` 或自 Metal 2.3 `[[kernel]]` 属性声明计算函数的语法。

```cpp
kernel void my_kernel(...) {...}

[[kernel]] void my_kernel2(...) {...}
```

使用 `kernel` 或 `[[kernel]]` 属性声明的函数必须返回 `void`。

你可以将 `[[max_total_threads_per_threadgroup]]` 函数属性与内核函数结合使用来指定每个线程组的最大线程数。

下面是使用此属性的内核函数的示例：

```cpp
[[max_total_threads_per_threadgroup(x)]] kernel void my_kernel(...) {
    // ...
}
```

如果 `[[max_total_threads_per_threadgroup]]` 值大于 `[MTLDevice maxThreadsPerThreadgroup]` 属性，则计算管道状态创建将失败。

## 可见函数

所有操作系统：自 Metal 2.3 起支持 `[[visible]]` 功能。

具有 `[[visible]]` 属性的函数是从 Metal Framework API 中可见的函数，即可以获取该函数的 `MTLFunction` 对象。获取可见函数的地址并获得可见函数指针是合法的。可见函数指针可以与 `visible_function_table` 类型一起使用（第 2.15 节）。其它函数直接调用 `visible` 函数是合法的。请注意，可见函数与其它 _qualified_ 函数一样，被分成自己的翻译单元。当函数直接调用 `visible` 函数时，开发人员需要将其传递到管道描述符中。

以下示例使用 `[[visible]]` 属性。

```cpp
[[visible]] float my_visible(device int *data, int data_offset) {
    // ...
}
```

## 可缝合函数

所有操作系统：自 Metal 2.4 起支持 `[[stitchable]]` 函数。

具有 `[[stitchable]]` 属性的函数是可以在 Metal Framework Function Stitching API 中使用的函数。`[[stitchable]]` 属性意味着 `[[visible]]`，这意味着可缝合函数可以在所有可以使用可见函数的上下文中使用，如第 5.1.4 节中所述。编译器将为可缝合函数生成额外的元数据，以使这些函数能够与 Metal Function Stitching API 一起使用。仅当开发人员需要此功能时才应使用此属性，因为元数据会增加函数的代码大小。

```cpp
[[stitchable]] float my_func(device float *data, texture2d<float> tex) {
    // ...
}
```

## 交集函数

所有操作系统：自 Metal 2.3 起支持 `[[intersection(primitive_type, intersection_tags...]]` 函数

可以使用 `[[intersection(primitive_type, intersection_tags...)]]` 属性声明一个与光线跟踪一起使用的自定义交集函数。当着色器调用 `intersect()`（参见第 6.18 节）时，会调用交集函数来确定潜在的光线交集是否有效或是否应继续遍历。Metal 支持以下类型的交集函数：

<TableView
  num="5.1"
  title="Intersection Function Primitive Types"
  headers={["Primitive Type", "Description"]}
  datasList={[
    [
      "triangle",
      "Indicates that this is an intersection function that extends the default triangle intersection test.",
    ],
    [
      "bounding_box",
      "Indicates that this is an intersection function which is run when a ray intersects the bounding box.",
    ],
    [
      "curve \nSince Metal 3.1",
      "Indicates that this is an intersection function that extends the default curve intersection test.",
    ],
  ]}
/>

你可以传递零个或多个交叉标记，如第 2.17 节中的表 2.9 中所述。一些例子是：

```cpp
[[intersection(triangle, triangle_data, instancing, world_space_data)]]
bool triangleIntersectionFunction(...) {...}

[[intersection(bounding_box, triangle_data, instancing, world_space_data)]]
UserResult boundingBoxIntersectionFunction(...) {...}
```

交集函数 `primitive_type` 和 `intersection_tags` 控制允许的输入和输出属性（参见第 5.2.3.7 节）。

交集函数支持从设备和常量地址空间传递缓冲区参数。

交集函数不支持将纹理参数传递给交集函数。但是，你可以使用参数缓冲区传递纹理。

交集函数不支持线程组内存。

交集函数不支持 threadgroup_barrier 或 simdgroup_barrier。如果使用它们，结果是不确定的。

交集函数可能会或可能不会在与启动交集操作的线程相同的 SIMD 组中运行：允许实现在启动 SIMD 组进行交集测试之前重新组合或重新打包候选交集，以提高效率。

如果加速结构遍历找到程序框基元，并且交集函数是三角形测试器（反之亦然），则这是应用程序错误并且行为未定义。

## 对象函数

所有操作系统：从 Metal 3 开始支持 `[[object]]` 函数。

具有 `[[object]]` 属性的函数是网格管道中的对象函数。对象函数是在 1-、2- 或 3D 计算网格上执行的数据并行函数，可以将计算网格启动到第二个网格阶段并带有数据负载。对象函数必须返回 `void`。

对象函数的输入内置变量在第 5.2.3.9 节中描述。`[[payload]]` 属性将对象函数导出到网格着色器的缓冲区标记为只读缓冲区。每个函数可以指定一次。

你可以将 `[[max_total_threads_per_threadgroup]]` 函数属性与对象函数结合使用来指定每个线程组的最大线程数。

你可以在对象函数上使用 `[[max_total_threadgroups_per_mesh_grid(size)]]` 来指定每个网格的最大线程组数。以下是使用 `[[object]]` 属性的示例。

```cpp
#define kMeshThreadgroups 32
struct ObjectOutput {
  // User-defined payload; one entry for each mesh threadgroup. This
  // is an array because the data will be shared by the mesh grid.
  float value[kMeshThreadgroups];
};

[[object, max_total_threadgroups_per_mesh_grid(kMeshThreadgroups)]] void objectShader(
      uint threadgroup_size [[threads_per_threadgroup]],
      uint lane [[thread_index_in_threadgroup]],
      object_data ObjectOutput& output [[payload]],
      mesh_grid_properties mgp
) {
    // ...
}
```

## 网格函数

所有操作系统：自 Metal 3 起支持 `[[mesh]]` 功能。

具有 `[[mesh]]` 属性的函数是网格管道中的网格函数。网格函数是一种数据并行函数，可以选择将表示几何块的网格对象导出到光栅化管道。网格对象是网格函数的参数。如果没有导出网格对象，则禁用光栅化。网格函数的输入内置变量在第 5.2.3.10 节中描述。网格函数必须返回 `void`。

你可以将 `[[max_total_threads_per_threadgroup]]` 函数属性与网格函数结合使用来指定每个线程组的最大线程数。以下是使用 `[[mesh]]` 属性的示例。

```cpp
struct vertex_t {
    float4 clip_pos [[position]];
    float3 world_pos;
    float3 color;
    // other user-defined properties
};
struct primitive_t {
    float3 normal;
};

// A mesh declaration that can export one cube.
using cube_mesh_t = metal::mesh<vertex_t, primitive_t,
                                8 /*corners*/,
                                6*2 /*faces*/,
                                metal::topology::triangle>;
struct view_info_t {
    float4x4 view_proj;
};

struct cube_info_t {
    float4x3 world;
    float3 color;
};

[[mesh, max_total_threads_per_threadgroup(12)]]
void cube_stage(cube_mesh_t output,
                const object_data cube_info_t &cube [[payload]],
                constant view_info_t &view [[buffer(0)]],
                uint gid [[threadgroup_position_in_grid]],
                uint lane [[thread_index_in_threadgroup]]
) {
    // ...
}
```

## 平铺功能

iOS：从 Metal 2 开始支持 Tile 函数。

macOS：从 Metal 2.3 开始支持 Tile 函数。

_图块着色函数_ 是一种特殊类型的计算内核或片段函数，可以与图形操作内联执行并利用基于图块的延迟渲染 (TBDR) 架构。使用 TBDR，命令会被缓冲，直到累积大量命令为止。硬件将帧缓冲区划分为图块，然后仅渲染每个图块内可见的图元。平铺着色函数支持在渲染过程中执行计算操作，这可以通过减少内存往返和利用高带宽本地内存来更有效地访问内存。

平铺函数启动一组称为 _dispatch_ 的线程，这些线程被组织成线程组和网格。你可以根据需要在渲染通道中的任何点启动线程。平铺函数会阻碍之前和后续的绘制，因此平铺函数在所有早期绘制完成之前不会执行。同样，在 tile 函数完成之前不会执行后面的绘制。

GPU 始终处理每个图块和每个调度直至完成。在处理下一个图块之前，所有绘制和分派都会在提交时启动图块。

平铺函数可以访问 32 KB 的线程组内存，该内存可以在图像块存储和线程组存储之间划分。（有关线程组内存大小的更多信息，请参阅第 4.4 节。）图像块大小取决于图块宽度、图块高度和每个样本的位深度。渲染通道附件（使用隐式图像块布局；请参阅第 5.6.3.1 节）或函数声明的结构（使用显式图像块布局；请参阅第 5.6.3.2 节）确定样本的位深度。有关内核函数如何利用 `threadgroup_imageblock` 地址空间的更多信息，请参阅第 4.5 节。

## 主机名属性

从 Metal 2.2 开始，你可以覆盖 Metal Framework API 用于引用限定函数的默认名称。将 `[[host_name(name)]]` 属性添加到函数声明中，其中 name 是 Metal Framework API 将用来引用函数名称的字符串文字。如果给不同的函数赋予相同的名称，编译器会引发编译时错误。例如，

```cpp
[[host_name("abc")]] [[kernel]] void funcA() {} // Metal API name is abc
[[host_name("xyz")]] [[kernel]] void funcX() {} // Metal API name is xyz
```

## 模板化限定函数

从 Metal 2.2 开始，你可以使用模板进行限定函数（例如顶点、片段、可见和内核函数）声明。你必须显式实例化模板以强制编译器为给定的专业化发出代码。例如，

```cpp
template<typename T>
kernel void bar(device T *x) {
    // ...
}

// Explicit specialization of `bar<T>` with [T = int]
template kernel void bar(device int *);
```

编译器为所有专业化提供相同的名称，除非使用 `[[host_name(name)]]` 属性为每个专业化提供不同的名称。

```cpp
// Explicit specialization of `bar<T>` with [T = int] and host_name
// "bar_int"
template [[host_name("bar_int")]] kernel void bar(device int *);

// Explicit specialization of `bar<T>` with [T = float] and host_name
// "bar_float"
template [[host_name("bar_float")]] kernel void bar(device float *);
```
